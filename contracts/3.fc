() recv_internal() {
}

int tlen(tuple t) asm "TLEN";
forall X -> (tuple, X) ~tpop (tuple t) asm "TPOP";
(int) ubitsize (int a) asm "UBITSIZE";

;; convert int bits to tuple
(tuple) int_to_bits(int v)
{
  var size = ubitsize(v);
  var c = begin_cell().store_int(v, size + 1).end_cell();
  var s = c.begin_parse();
  s~load_uint(1);

  var list = empty_tuple();
  repeat (size)
  {
    list~tpush(s~load_uint(1));
  }

  return list;
}

;; write first items of t to builder
(builder, tuple) store_to_builder_t(builder b, tuple builders, tuple t)
{
  var len = t.tlen();
  
  if ((builder_bits(b) + len) >= 1023)
  {
    builders~tpush(b);
    b = begin_cell();
  }

  var i = 0;
  repeat (len)
  {
    b~store_uint(t.at(i), 1);
    i += 1;
  }
  
  return (b, builders);
}

(tuple) build_flow(tuple flag, tuple value, cell linked_list)
{
  var flen = flag.tlen();
  
  var temp = empty_tuple();
  var tl = 0;
  var cache = empty_tuple();
  
  var sl = linked_list.begin_parse();
  var b = begin_cell();
  var builders = empty_tuple();
  
  var continue = slice_bits(sl) > 0;
  while(continue)
  {
    while (slice_bits(sl) > 0)
    {
      int next = 0;
      
      if (cache.tlen() > 0)
      {
        next = cache~tpop();
      }
      else
      {
        next = sl~load_uint(1);
      }
      
      if (next == flag.at(tl))
      {
        temp~tpush(next);
        tl += 1;
        
        if (tl == flen)
        {
          (b, builders) = store_to_builder_t(b, builders, value);
          temp = empty_tuple();
          tl = 0;
        }
      }
      else
      {
        if (tl > 0)
        {
          if (builder_bits(b) < 1023)
          {
            b~store_uint(temp.at(0), 1);
          }
          else
          {
            builders~tpush(b);
            b = begin_cell().store_uint(temp.at(0), 1);
          }
          
          temp~tpush(next);
          
          repeat (tl){
            var t = temp~tpop();
            cache~tpush(t);
          }

          temp = empty_tuple();
          tl = 0;
        }
        else
        {
          if (builder_bits(b) < 1023)
          {
            b~store_uint(next, 1);
          }
          else
          {
            builders~tpush(b);
            b = begin_cell().store_uint(next, 1);
          }
        }
      }
    }
    
    if (slice_refs(sl) > 0)
    {
      sl = (sl~load_ref()).begin_parse();
    }
    else
    {
      continue = false;
    }
  }
  
  ;; Set other temp
  if (tl > 0)
  {
    if ((builder_bits(b) + tl) >= 1023)
    {
      builders~tpush(b);
      b = begin_cell();
    }
    var i = 0;
    repeat (tl)
    {
      b~store_uint(temp.at(i), 1);
      i += 1;
    }
  }

  ;; Set other cache
  repeat (cache.tlen())
  {
    if (builder_bits(b) < 1023)
    {
      b~store_uint(cache~tpop(), 1);
    }
    else
    {
      builders~tpush(b);
      b = begin_cell().store_uint(cache~tpop(), 1);
    }
  }

  builders~tpush(b);
  
  return builders;
}

;; testable
(cell) find_and_replace(int flag, int value, cell linked_list) method_id 
{
  ;; Ok for opt
  if ((flag <= 0) | (value <= 0)){ return null(); }
  ;; Ok for opt
  var tupleF = int_to_bits(flag);
  var tupleV = int_to_bits(value);
  ;; Create builders
  var builders = build_flow(tupleF, tupleV, linked_list);
  ;; To tree
  var b = begin_cell();
  repeat (builders.tlen())
  {
    var nt = builders~tpop();
    b = nt.store_ref(b.end_cell());
  }
  ;; Return
  return b.end_cell();
}
