{-
  TASK 1 - Find branch of the cell tree
  Write the method that will find a branch of the tree by comparing its
  hash with the hash received in the first parameter. When the algorithm finds
  the subtree (branch) whose hash equals the received hash, the root cell of
  this branch should be returned. Return empty cell if the branch is not found.
-}


() recv_internal() 
{
}

forall X -> int is_null (X x) asm "ISNULL";

;; testable
(cell) find_branch_by_hash(int hash, cell tree) method_id 
{
  var list = cons(tree, null());

  while (is_null(list) == 0)
  {
    var part = list~list_next();
    if (cell_hash(part) == hash)
    {
      return part;
    }

    var to_slice = part.begin_parse();
    repeat (slice_refs(to_slice))
    {
      list = cons(to_slice~load_ref(), list);
    }
  }

  return begin_cell().end_cell();
}
