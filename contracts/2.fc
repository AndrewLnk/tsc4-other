{-
  TASK 2 - Matrix multiplier
  Write the method that multiplies one matrix by another.
  The 1st and 2nd parameter of the function will contain
  a matrix implemented using tuples (eg: [[1,2], [3,4]]).
  Matrices can be of different sizes, ranging from 4x4
  up to 32x32.
  Matrix can be rectangular where matrixA must to be of size n*m & matrixB of size m*p.
  After calculating the result matrix, the function should return final result (as tuple).
-}

() recv_internal() {
}

;; From telegram chat (get length of a tuple)
int tlen(tuple t) asm "TLEN"; 

;; From telegram chat (get element from a tuple at position) cell -> tuple
tuple tuple_at_index(tuple t, int index) asm "INDEXVAR";

;; From telegram chat (get element from a tuple at position) cell -> int
int value_at_index(tuple t, int index) asm "INDEXVAR";

;; testable
(tuple) matrix_multiplier(tuple matrixA, tuple matrixB) method_id 
{
  var matrixC = empty_tuple();

  var mC_x_lenth = tlen(tuple_at_index(matrixB, 0));
  var mC_y_lenth = tlen(matrixA);
  var mA_y_lenth = tlen(tuple_at_index(matrixA, 0));
 
  var y = 0;

  while(y < mC_y_lenth)
  {
    var rowC = empty_tuple();

    var target_y_C = 0;
    while (target_y_C < mC_x_lenth)
    {
      var x = 0;
      var temp = 0;

      while(x < mA_y_lenth)
      {
        temp += value_at_index(tuple_at_index(matrixA, y), x) * value_at_index(tuple_at_index(matrixB, x), target_y_C);
        x += 1;
      }

      rowC~tpush(temp);
      target_y_C += 1;
    }

    matrixC~tpush(rowC);
    y += 1;
  }

  return matrixC;
}

