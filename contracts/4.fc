() recv_internal() {
}

forall X -> (tuple) to_tuple (X x) asm "NOP";

builder entry(builder b, slice sl, tuple table)
{
  var r = slice_bits(sl) / 8;
  repeat (r)
  {
    b~store_uint(table.at(sl~load_uint(8)), 8);
  }

  if (~ slice_refs_empty?(sl))
  {
    var ns = sl~load_ref().begin_parse();
    var nb = entry(begin_cell(), ns, table);
    b = b.store_ref(nb.end_cell());
    return b;
  }
  else
  {
    return b;
  }
}

tuple create_table(int shift)
{
  tuple table = empty_tuple();

  var i = 0;
  repeat (255)
  {
    if ((i >= 65) & (i <= 90)) 
    {
       table~tpush((i - 65 + shift) % 26 + 65);
    }
    else
    {
      if ((i >= 97) & (i <= 122))
      {
        table~tpush((i - 97 + shift) % 26 + 97);
      }
      else
      {
        table~tpush(i);
      }
    }
    
    i += 1;
  } 

  return table;
}

;; testable
(cell) caesar_cipher_encrypt(int shift, cell text) method_id
{
  var s = text.begin_parse();
  var sl = s~load_uint(32);
  var b = begin_cell().store_uint(sl, 32);

  tuple table = create_table(shift);
  return entry(b, s, table).end_cell();
}

;; testable
(cell) caesar_cipher_decrypt(int shift, cell text) method_id
{
  var s = text.begin_parse();
  var sl = s~load_uint(32);
  var b = begin_cell().store_uint(sl, 32);
  
  tuple table = create_table(0 - shift);
  return entry(b, s, table).end_cell();
}