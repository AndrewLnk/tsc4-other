() recv_internal() {
}

forall X -> (tuple, X) ~tpop (tuple t) asm "TPOP";

(cell) entry(builder b, slice sl, int s)
{
  var builders = empty_tuple();
  var builders2 = empty_tuple();
  
  var passed = 0;

  var continue = true;
  while (continue)
  {
    var r = slice_bits(sl) / 8;
    repeat (r)
    {
      var c = sl~load_uint(8);
      var sh = shifting(c, s);
      b = b.store_uint(sh, 8);
    }
    
    if (passed >= 254)
    {
      builders2~tpush(b);
    }
    else
    {
      builders~tpush(b);
    }

    if (slice_refs(sl) != 0)
    {
      var next_sl = sl~load_ref().begin_parse();
      sl = next_sl;
      b = begin_cell();
    }
    else
    {
      continue = false;
    }

    passed += 1;
  }
  
  if (passed >= 255)
  {
    var b = builders2~tpop();
    repeat (passed - 255)
    {
      var nt = builders2~tpop();
      b = nt.store_ref(b.end_cell());
    }
    repeat (254)
    {
      var nt = builders~tpop();
      b = nt.store_ref(b.end_cell());
    }

    return b.end_cell();
  }
  else
  {
    var b = builders~tpop();
    repeat (passed - 1)
    {
      var nt = builders~tpop();
      b = nt.store_ref(b.end_cell());
    }
    
    return b.end_cell();
  }
}

(int) shifting(int c, int s)
{
  if ((c >= 97) & (c <= 122))
  {
    return (c + s - 97) % 26 + 97;
  }
  else
  {
    if ((c >= 65) & (c <= 90))
    {
      return (c + s - 65) % 26 + 65;
    }

    return c;
  }
}

;; testable
(cell) caesar_cipher_encrypt(int shift, cell text) method_id
{
  var s = text.begin_parse();
  var sl = s~load_uint(32);
  var b = begin_cell().store_uint(sl, 32);

  return entry(b, s, shift);
}

;; testable
(cell) caesar_cipher_decrypt(int shift, cell text) method_id
{
  var s = text.begin_parse();
  var sl = s~load_uint(32);
  var b = begin_cell().store_uint(sl, 32);

  return entry(b, s, 0 - shift);
}